plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id "org.jetbrains.kotlin.kapt" version "1.4.21"
}

group = 'com.druk'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

task swiftBuild(type: Exec) {
    environment("TOOLCHAINS", "")
    workingDir 'src/main/swift'
    commandLine 'swift', 'build'
}

task swiftPackageClean(type: Exec) {
    environment("TOOLCHAINS", "")
    workingDir 'src/main/swift'
    commandLine 'swift', 'package', 'clean'
}

task swiftPackageUpdate(type: Exec) {
    environment("TOOLCHAINS", "")
    workingDir 'src/main/swift'
    commandLine 'swift', 'package', 'update'
}

compileJava.finalizedBy(swiftBuild)

test {
    systemProperty "java.library.path", file("${buildDir}/../src/main/swift/.build/debug").absolutePath
}

dependencies {
    kapt "com.readdle.swift.java.codegen:compiler:0.9.2"
    implementation "com.readdle.swift.java.codegen:annotations:0.9.2"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

test {
    useJUnit()
}

kapt {
    arguments {
        arg("com.readdle.codegen.package", """{
                   "moduleName": "SampleProject",
                   "importPackages": [],
                   "customTypeMappings": {
                           "com.readdle.swiftjava.sample.CustomSampleValue": "SampleValue"
                   }
               }
               """)
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

application {
    mainClassName = 'MainKt'
}